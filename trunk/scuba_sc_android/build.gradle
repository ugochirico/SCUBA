buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.0'
  }
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'

version = "0.0.12"
group = "net.sf.scuba"
// artifactId is derived from rootProject.name as specified in settings.gradle

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.3"

  defaultConfig {
    minSdkVersion 10
    targetSdkVersion 24
  }

  lintOptions {
      abortOnError false
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }
}

dependencies {
  compile 'net.sf.scuba:scuba-smartcards:0.0.11'
}

task copyJar(type: Copy, dependsOn:':bundleRelease') {
  from "$buildDir/intermediates/bundles/release"
  into "$buildDir/outputs/libs" 
  include('classes.jar')
  rename ('classes.jar', "${rootProject.name}-${version}" + '.jar')
}

build.dependsOn(copyJar)

signing {
//  required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
  required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }

  sign configurations.archives
}

uploadArchives {
  configuration = configurations.archives
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: sonatypeRepo) {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    pom.project {
      name 'SCUBA SC Android'
      packaging 'aar'
      description 'Smart Card Utils for Better Access - Android.'
      url 'http://scuba.sourceforge.net'

      scm {
        url 'https://sourceforge.net/projects/scuba/'
        connection 'scm:svn:https://svn.code.sf.net/p/scuba/code'
      }

      licenses {
        license {
          name 'GNU Library or Lesser General Public License (LGPL)'
          url 'http://www.gnu.org/licenses/lgpl.html'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'martijno'
          name 'Martijn Oostdijk'
          email 'martijn.oostdijk@gmail.com'
        }
      }
    }
  }
}
