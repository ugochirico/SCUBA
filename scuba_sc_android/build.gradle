buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.2.3'
  }
}

allprojects {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'

version = "0.0.10"
group = "net.sf.scuba"
// artifactId is derived from rootProject.name as specified in settings.gradle

android {
  compileSdkVersion 22
  buildToolsVersion "23.0.2"

  defaultConfig {
    minSdkVersion 10
    targetSdkVersion 22
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }
}

dependencies {
  compile 'net.sf.scuba:scuba-smartcards:0.0.9'
}

task copyJar(type: Copy, dependsOn:':bundleRelease') {
  from "$buildDir/intermediates/bundles/release"
  into "$buildDir/outputs/libs" 
  include('classes.jar')
  rename ('classes.jar', "${rootProject.name}-${version}" + '.jar')
}

build.dependsOn(copyJar)

signing {
  required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  configuration = configurations.archives
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: sonatypeRepo) {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    pom.project {
      name 'SCUBA SC Android'
      packaging 'aar'
      description 'Smart Card Utils for Better Access - Android.'
      url 'http://scuba.sourceforge.net'

      scm {
        url 'https://sourceforge.net/projects/scuba/'
        connection 'scm:svn:https://svn.code.sf.net/p/scuba/code'
      }

      licenses {
        license {
          name 'GNU Library or Lesser General Public License (LGPL)'
          url 'http://www.gnu.org/licenses/lgpl.html'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'martijno'
          name 'Martijn Oostdijk'
          email 'martijn.oostdijk@gmail.com'
        }
      }
    }
  }
}
